mixed rwa_validator

import { get_current_input, tx } from ScriptContext

struct State {
    current_supply:             Int       "current_supply"
    last_mint_supply:           Int       "last_mint_supply"
    last_deposit_or_withdrawal: ByteArray "last_deposit_or_withdrawal" // this is hash, because it might contain data that shouldn't be public

    reserves_account: ByteArray "reserves_account" // this is a hash, because it might contain private data, can't change

    name:        String "name"
    description: String "description"
    decimals:    Int    "decimals"    // can't change
    ticker:      String "ticker"      // can't change
    url:         String "url"
    logo:        String "logo"
}

enum Cip68Extra {
    Unused
}

enum Metadata {
    Cip68 {
        state:   State
        version: Int
        extra:   Cip68Extra
    }
    
    func state(self) -> State {
        self.switch{
            x: Cip68 => x.state
        }
    }
}

struct Redeemer {
    total_reserves:                    Real // usually a USD value, 6 decimal places is enough
    reserves_change:                   Real
    latest_deposit_or_withdrawal_hash: ByteArray
}

const SEED_ID          = TxOutputId::new(TxId::new(#), 0)
const RESERVES_ACCOUNT = #
const ORACLE_KEYS      = []PubKeyHash{}
const INITIAL_PRICE: Real = 0.001 // 1000 USD per token -> 0.001 USD per microtoken
const TICKER           = "USDT"
const NAME             = TICKER + " RWA"
const DESCRIPTION      = TICKER + " RWA operated by PBG"
const DECIMALS         = 6
const URL              = "https://www.pbg.io"
const LOGO             = "https://assets.pbg.io/usdt_bridge.png"

const ticker_bytes    = TICKER.encode_utf8()
const user_token_name = Cip67::fungible_token_label + ticker_bytes
const ref_token_name  = Cip67::reference_token_label + ticker_bytes

const own_hash    = Scripts::rwa_validator
const own_mph     = MintingPolicyHash::from_script_hash(own_hash)
const own_address = Address::new(
    SpendingCredential::new_validator(
        ValidatorHash::from_script_hash(own_hash)
    ),
    Option[StakingCredential]::None
)

const ref_token_asset_class  = AssetClass::new(own_mph, ref_token_name)
const user_token_asset_class = AssetClass::new(own_mph, user_token_name)

func validate_initialization() -> () {
    assert(tx.inputs.any((input: TxInput) -> {
        input.output_id == SEED_ID
    }), "seed UTXO not spent")

    ref_utxo = tx.outputs.find((output: TxOutput) -> {
        output.address == own_address
        && output.value.get_safe(ref_token_asset_class) == 1
    })

    metadata = ref_utxo.datum.inline.as_strictly[Metadata]

    assert(metadata == Metadata::Cip68{
        State{
            current_supply:             0,
            last_mint_supply:           0,
            last_deposit_or_withdrawal: #,
            
            reserves_account: RESERVES_ACCOUNT,
            name:             NAME,
            description:      DESCRIPTION,
            decimals:         DECIMALS,
            ticker:           TICKER,
            url:              URL,
            logo:             LOGO
        },
        1,
        Cip68Extra::Unused
    }, "metadata not initialized correctly")
}

func signed_by_quorum() -> Bool {
    n_signers = ORACLE_KEYS.fold((n_signers: Int, key: PubKeyHash) -> {
        n_signers + tx.is_signed_by(key).to_int()
    }, 0)

    n_signers > (ORACLE_KEYS.length/2)
}

func calc_token_price(R: Real, Delta: Real, current_supply: Int, last_mint_supply: Int) -> Real {
    if (current_supply == 0 || last_mint_supply == 0) {
        INITIAL_PRICE
    } else {
        p_last = (R - Delta)/last_mint_supply
        p_curr = R / current_supply

        if (p_last < p_curr) {
            p_last
        } else {
            p_curr
        }
    }
}

func validate_state_change(redeemer: Redeemer, input: TxInput) -> () {
    state0 = input.datum.inline.as[Metadata].state()

    output = tx.outputs.find((output: TxOutput) -> {
        output.address == own_address
        && output.value.get_safe(ref_token_asset_class) > 0
    })

    state1 = output.datum.inline.as_strictly[Metadata].state()

    // ensure constant metadata fields don't change
    assert(state1.reserves_account == state0.reserves_account, "reserves account not constant")
    assert(state1.ticker == state0.ticker, "metadata ticker not constant")
    assert(state1.decimals == state0.decimals, "metadata decimals not constant")

    n = tx.minted.get_safe(user_token_asset_class)
    N0 = state0.current_supply
    N1 = state1.current_supply

    assert((N1 - N0) == n, "current token supply not updated correctly")
    
    if (n > 0) {
        Redeemer{R, Delta, h} = redeemer

        p = calc_token_price(R, Delta, N0, state0.last_mint_supply)

        assert(N1*p <= R, "too many tokens minted")
        assert(state1.last_deposit_or_withdrawal == h, "last_deposit_or_withdrawal not updated correctly")
        assert(state1.last_mint_supply == N1, "last_mint_supply not updated correctly")
        assert(signed_by_quorum(), "not signed by simple quorum of oracles")
    } else {
        assert(state1.last_mint_supply == state0.last_mint_supply, "last_mint_supply can't change during burn")
        assert(state1.last_deposit_or_withdrawal == state0.last_deposit_or_withdrawal, "last_deposit_or_withdrawal can't change during burn")
    }
}

func main(args: MixedArgs) -> () {
    args.switch{
        s: Spending => {
            redeemer = s.redeemer.as[Redeemer]

            utxo = get_current_input()

            tokens = utxo.value.get_policy(own_mph)

            if (tokens.is_empty()) {
                // UTXOs that don't contain any tokens from current policy can always be spent.
                // This can be used to remove garbage.
                ()
            } else {
                (name, _) = tokens.head

                if (name == ref_token_name) {
                    validate_state_change(redeemer, utxo)
                } else {
                    error("unexpected token name")
                }
            }
        },
        Other => {
            tokens = tx.minted.get_policy(own_mph)

            (name, qty) = tokens.head
            tail = tokens.tail

            assert(tail.is_empty(), "only one token kind can be minted or burned")

            if (name == user_token_name && qty != 0) {
                // metadata token must be spent, which triggers Spending witness
                assert(tx.inputs.any((input: TxInput) -> {
                    input.address == own_address
                    && input.value.get_safe(ref_token_asset_class) > 0
                }), "ref token not spent")
            } else if (qty == 1 && (name == ref_token_name)) {
                validate_initialization()
            } else {
                error("invalid minted tokens")
            }
        }
    }
}